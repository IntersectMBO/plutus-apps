{ runCommand, fix-purs-tidy, src, lib, diffutils, glibcLocales }:
let
  # just purescript sources
  src' = lib.cleanSourceWith {
    inherit src;
    filter = with lib;
      name: type:
        let baseName = baseNameOf (toString name); in
        (
          baseName == ".tidyoperators" ||
          baseName == ".tidyrc.json" ||
          (type == "regular" && hasSuffix ".purs" baseName) ||
          type == "directory"
        );
  };
in
runCommand "purs-tidy-check"
{
  buildInputs = [ fix-purs-tidy diffutils glibcLocales ];
} ''
  set +e
  echo ${toString src'}
  cp -a ${src'} orig
  cp -a ${src'} purs-tidy
  chmod -R +w purs-tidy
  cd purs-tidy
  fix-purs-tidy
  cd ..
  diff --brief --recursive orig purs-tidy > /dev/null
  EXIT_CODE=$?
  if [[ $EXIT_CODE != 0 ]]
  then
    mkdir -p $out/nix-support
    diff -ur orig purs-tidy > $out/purs-tidy.diff
    echo "file none $out/purs-tidy.diff" > $out/nix-support/hydra-build-products
    echo "*** purs-tidy found changes that need addressed first"
    echo "*** Please run \`nix-shell --run fix-purs-tidy\` and commit changes"
    echo "*** or apply the diff generated by hydra if you don't have nix."
    exit $EXIT_CODE
  else
    echo $EXIT_CODE > $out
  fi
''
